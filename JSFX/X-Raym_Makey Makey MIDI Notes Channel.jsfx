/**
 * JSFX Name: Makey Makey MIDI Notes Channel
 * Description: Map Makey Makey inputs to specific MIDI Channels.
 * About:
    Based on MineMooder Makey Makey MIDI controller Instructables Tutorial.
    Place this before the JS: Makey Makey MIDI Notes Map.
 * Screenshot:
 * Author: X-Raym
 * Author URI: http://www.extremraym.com
 * Links:
     MineMooder Makey Makey MIDI controller Instructables Tutorial http://www.instructables.com/id/Makey-Makey-MIDI-controller/
     Forum Thread
 * Donation: http://www.extremraym.com/en/donation
 * Licence: GPL v3
 * REAPER: 5.0
 * Extensions: None
 * Version: 1.0
 */

/**
 * Changelog:
 * v1.0 (2016-09-02)
  + Initial Release
 */

desc:Makey Makey MIDI Notes Channel

slider1:0<0,16,1{Any,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>Input Channel

slider10:1<1,16,1>Up
slider11:1<1,16,1>Down
slider12:1<1,16,1>Left
slider13:1<1,16,1>Right
slider14:1<1,16,1>Space
slider15:1<1,16,1>Click

slider30:1<1,16,1>W
slider31:1<1,16,1>A
slider32:1<1,16,1>S
slider33:1<1,16,1>D
slider34:1<1,16,1>F
slider35:1<1,16,1>G

slider50:1<1,16,1>A5 - Mouse Up
slider51:1<1,16,1>A4 - Mouse Down
slider52:1<1,16,1>A3 - Mouse Left
slider53:1<1,16,1>A2 - Mouse Right
slider54:1<1,16,1>A1 - Mouse Click Left
slider55:1<1,16,1>A0 - Mouse Click Right


////////////////////////////////////////////////////////////////////////////////
@init
statNoteOn = $x90;
statNoteOff = $x80;

slider_dest = 0;

slider_dest[60] = 10;
slider_dest[61] = 11;
slider_dest[62] = 12;
slider_dest[63] = 13;
slider_dest[64] = 14;
slider_dest[65] = 15;
slider_dest[66] = 30;
slider_dest[67] = 31;
slider_dest[68] = 32;
slider_dest[69] = 33;
slider_dest[70] = 34;
slider_dest[71] = 35;
slider_dest[72] = 50;
slider_dest[73] = 51;
slider_dest[74] = 52;
slider_dest[75] = 53;
slider_dest[76] = 54;
slider_dest[77] = 55;

////////////////////////////////////////////////////////////////////////////////
@slider
inChannel = slider1 - 1;

////////////////////////////////////////////////////////////////////////////////
@block

while
(

  midirecv(offset,msg1,note,vel) ?
  (
    // Extract message type and channel
    status = msg1 & $xF0;
    channel = msg1 & $x0F;

    // Is it on our channel?
    channel == inChannel || inChannel == -1 ?
    (
    
      // Is it a note event?
      status == statNoteOn || status == statNoteOff ?
      (

        // If note is in Makey Makey MIDI Range
        note >= 60 && note <= 77   ? (

          chan_out = slider( slider_dest[note] ) - 1; // Get Note Out

          msg1 =  status + chan_out;

        );
     
      );
    
    );
    
    midisend(offset, msg1, note, vel);
    
    1; // Force loop to continue until all messages have been processed
  
  );

);
