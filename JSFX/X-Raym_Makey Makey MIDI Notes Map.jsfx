/**
 * JSFX Name: Makey Makey MIDI Notes Map
 * Description: Map Makey Makey inputs to specific MIDI notes.
 * About: Based on MineMooder Makey Makey MIDI controller Instructables Tutorial.
 * Screenshot: https://youtu.be/Co3ttlSlMlY
 * Author: X-Raym
 * Author URI: http://www.extremraym.com
 * Links:
     Article http://www.extremraym.com/en/makey-makey-midi-jsfx/
     MineMooder Makey Makey MIDI controller Instructables Tutorial http://www.instructables.com/id/Makey-Makey-MIDI-controller/
     Forum Thread http://forum.cockos.com/showthread.php?t=181102
 * Donation: http://www.extremraym.com/en/donation
 * Licence: GPL v3
 * REAPER: 5.0
 * Extensions: None
 * Version: 1.0.1
 */

/**
 * Changelog:
 * v1.0.1 (2016-09-02)
  # Prevent double note off events
 * v1.0 (2016-09-02)
  + Initial Release
 */

desc:Makey Makey MIDI Notes Map

slider1:0<0,16,1{Any,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>Input Channel

slider10:60<0,127,1>Up
slider11:61<0,127,1>Down
slider12:62<0,127,1>Left
slider13:63<0,127,1>Right
slider14:64<0,127,1>Space
slider15:65<0,127,1>Click

slider30:66<0,127,1>W
slider31:67<0,127,1>A
slider32:68<0,127,1>S
slider33:69<0,127,1>D
slider34:70<0,127,1>F
slider35:71<0,127,1>G

slider50:72<0,127,1>A5 - Mouse Up
slider51:73<0,127,1>A4 - Mouse Down
slider52:74<0,127,1>A3 - Mouse Left
slider53:75<0,127,1>A2 - Mouse Right
slider54:76<0,127,1>A1 - Mouse Click Left
slider55:77<0,127,1>A0 - Mouse Click Right


////////////////////////////////////////////////////////////////////////////////
@init
statNoteOn = $x90;
statNoteOff = $x80;

slider_dest = 0;

slider_dest[60] = 10;
slider_dest[61] = 11;
slider_dest[62] = 12;
slider_dest[63] = 13;
slider_dest[64] = 14;
slider_dest[65] = 15;
slider_dest[66] = 30;
slider_dest[67] = 31;
slider_dest[68] = 32;
slider_dest[69] = 33;
slider_dest[70] = 34;
slider_dest[71] = 35;
slider_dest[72] = 50;
slider_dest[73] = 51;
slider_dest[74] = 52;
slider_dest[75] = 53;
slider_dest[76] = 54;
slider_dest[77] = 55;

note_buf = 100;

////////////////////////////////////////////////////////////////////////////////
@slider
inChannel = slider1 - 1;

////////////////////////////////////////////////////////////////////////////////
@block

while
(

  midirecv(offset,msg1,note,vel) ?
  (

    // Extract message type and channel
    status = msg1 & $xF0;
    channel = msg1 & $x0F;

    // Is it on our channel?
    channel == inChannel || inChannel == -1 ?
    (
      
      // Is it a note event?
      status == statNoteOn || status == statNoteOff ?
      (

        // If note is in Makey Makey MIDI Range
        note >= 60 && note <= 77   ? (

          note = slider( slider_dest[note] ); // Get Note Out
        /*
          note_buf[note] ? ( // If Note already in buffer, stop it. Prevent note hang if Stop after tranposing while note playing.
            midisend(offset, statNoteOff | channel, note);
            note_buf[note] =  0;
          );

          status == statNoteOn ? (
            note_buf[note] =  1; // Store note time in buffer.
          ):(
            note_buf[note] =  0;
          );

        */  

        );
     
      );
    
    );
    
    midisend(offset, msg1, note, vel);
    
    1; // Force loop to continue until all messages have been processed
  
  );

);
